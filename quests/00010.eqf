Main
{
	questname	"Guard Job(rep)"
	version		1
}

state Begin
{
	desc		"Speak to Guard, Stronghold swamp"

	action		AddNpcText(10, "Hi, im here to guard this cave entrance, quite many smugglers from Anundo are trying to bring illegal fairy soda into Aeven.");
	action		AddNpcText(10, "I've been here for quite long and those spiders bit me several times, I wish someone could do something about these spiders..");

	rule		TalkedToNpc(10) goto HelpGuard

}

state HelpGuard
{
	desc		"Kill Spiders, Stronghold swamp"

	action		ShowHint("Quest reward: 6050 EXP and 300 Gold!");

	action		AddNpcText(10, "I'll pay 300 gold and 6050 exp for every 20 spiders killed.");

	action		AddNpcInput(10, 1, "I'll get it done!");
	action		AddNpcInput(10, 2, "I'm busy atm..");

	rule 		InputNpc(1) goto KillSpiders
	rule		InputNpc(2) goto Reset
}

state KillSpiders
{
	desc		"Kill 20 Spiders"

	action		AddNpcChat(10, "I'll pay 300 gold and 6050 exp for every 20 spiders killed.");
	action		AddNpcText(10, "Come and see me once you have killed those 20 spiders.");

	action		ShowHint("Kill 20 Spiders then return to Guard!");
	action		PlaySound(18);

	rule		KilledNpcs(10,20) goto KilledSpiders
}

state KilledSpiders
{
	desc		"Speak to Guard, Stronghold swamp"

	action		ShowHint("You've successfully killed 20 Spiders, return to Guard!");
	action		PlaySound(18);

	action		AddNpcText(10, "So, you killed 20 swamp spiders already? I am sorry but I don't believe you.. Bring me at least 10 spider legs to prove it.");
	rule		TalkedToNpc(10) goto CollectLegs
}

state CollectLegs
{
	desc		"Collect 10 Spider Legs"

	action		ShowHint("Collect 10 Spider Legs and return to Guard!");
	action		PlaySound(17);

	action		AddNpcChat(10, "Nobody can kill 20 spiders that quickly. You must be lying!");
	action		AddNpcText(10, "Come and see me once you have proof of your kills.");

	rule		GotItems(257, 10) goto GotProof
}

state GotProof
{
	desc		"Speak to Guard, Stronghold swamp"

	action		ShowHint("You've collected 10 Spider Legs, return to Guard!");
	action		PlaySound(18);

	action		AddNpcText(10, "You really did kill those spiders? Lets see those Spider Legs..");

	action		AddNpcInput(10, 1, "Ok, look!");
	action		AddNpcInput(10, 2, "Naa ill keep them");

	rule 		InputNpc(1) goto GotoCastle
	rule 		InputNpc(2) goto Reset
	rule		LostItems(257, 10) goto CollectLegs
}

state GotoCastle
{
	desc		"Speak with Captain, Castle"

	action		RemoveItem(257, 10);
	action		ShowHint("You have given 10 Spider Legs to the Guard");

	action		AddNpcText(10, "Hmm I see.. Look for my Captain at the Imperial Castle, he will reward you for your efforts.");

	action		AddNpcText(11, "I heard that you have been assisting one of our Imperial Guards in the swamps.");
	action		AddNpcText(11, "I will reward you with some combat experience and gold for your brave services.");

	rule		TalkedToNpc(11) goto Reward
}

state Reset
{
	action		ShowHint("Very well, move along civilian..");
	action		Reset();
}

state Reward
{
	action		GiveExp(6050);
	action		GiveItem(1, 300);
	action		ShowHint("[Quest Complete] You gained 6050 EXP and 300 gold!!");
	action		PlaySound(97);
	action		Reset();
}